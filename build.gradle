apply plugin: 'base'
apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'war'

task wrapper(type: Wrapper) {
	gradleVersion = '2.0'
}

project.webAppDirName = 'WebContent'

sourceSets {
    main {
        java { 
            srcDir 'src'
            exclude '**/test/**'
         }
        resources { srcDir 'src' }
    }
    test {
        java {
            srcDir 'src'    
        }
        
    }
}

test {
        systemProperties = System.properties
        testLogging.showStandardStreams = true

        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }

        // listen to standard out and standard error of the test JVM(s)
        onOutput { descriptor, event ->
            logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
        }
}


war {
	destinationDir = file("$buildDir/dist")
    archiveName = "Struts2CRUD.war"
}

eclipse {
	jdt {
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
	}
	wtp {
        facet {
            facet name: 'jst.java', version: '1.7'
            facet name: 'jst.web', version: '3.0'
            facet name: 'wst.jsdt.web', version: '1.0'
        }
    }
    classpath{
    	containers.clear()
    	containers
            .add('org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7')
    	file{
    		beforeMerged { classpath ->
    			def tomcatRuntimeContainer \
                        = new org.gradle.plugins.ide.eclipse.model.Container('org.eclipse.jst.server.core.container/org.eclipse.jst.server.tomcat.runtimeTarget/Apache Tomcat v7.0')
    			classpath.entries << tomcatRuntimeContainer
    		}
    	}
    }
}

repositories {
    mavenCentral()
}
 
dependencies {
    // struts 2
    compile group:'org.apache.struts',name:'struts2-core', version: '2.3.16.3+'
    // log
    compile 'commons-logging:commons-logging:1.1.3'
    compile 'commons-logging:commons-logging-api:1.1'
    // test
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

